buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
    maven {
        name = "JEI"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        name = "MCMP"
        url = "http://maven.amadornes.com/"
    }
    maven {
        name = "OpenComputers"
        url = "http://maven.cil.li/"
    }
    maven {
        name = "ComputerCraft"
        url = "http://cc.crzd.me/maven"
    }
    maven {
        name = "hwyla"
        url = 'http://tehnut.info/maven/'
    }
    maven { // TOP
        name = "tterrag maven"
        url = "http://maven.tterrag.com/"
    }
    maven {
        name 'BlameJared - Crafttweaker'
        url 'http://maven.blamejared.com/'
    }
	maven {
		name = "K-4u Mod maven"
		url = "https://modmaven.k-4u.nl/"
	}
}

task copyCIArtifacts << {
    if (System.getenv("CIRCLECI").equals("true")) {
        copy {
            from(file('build/libs/').listFiles())
            into System.getenv('CIRCLE_ARTIFACTS')
        }
    }
}

// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
version = "${mc_version}-${mod_version_massive}.${mod_version_major}.${mod_version_minor}"
group = "me.desht.pneumaticcraft"
archivesBaseName = "pneumaticcraft-repressurized"

if (System.getenv().CIRCLE_BUILD_NUM != null) {
    version += "-${System.getenv().CIRCLE_BUILD_NUM}"
} else {
    version += "-CUSTOM"
}

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

// prevent java 8's strict doclint for javadocs from failing builds
allprojects {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

jar {
	from sourceSets.main.output
	from sourceSets.api.output
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allJava
	from sourceSets.api.allJava
}

task apiJar(type: Jar) {
    classifier = 'api'
    from sourceSets.api.java
    // TODO: when FG bug is fixed, remove allJava from the api jar.
	// https://github.com/MinecraftForge/ForgeGradle/issues/369
	// Gradle should be able to pull them from the -sources jar.
	from sourceSets.api.allJava
}

artifacts {
    archives sourcesJar
    archives apiJar
}

compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${forge_version}"
    runDir = "run"
    mappings = "${mappings_version}"
    clientJvmArgs += "-Dfml.coreMods.load=elucent.albedo.asm.FMLPlugin"
    replace '@VERSION@', project.version
        replaceIn 'PneumaticCraftRepressurized.java'
    replace '@MASSIVE@', mod_version_massive
        replaceIn 'lib/Versions.java'
    replace '@MAJOR@', mod_version_major
        replaceIn 'lib/Versions.java'
    replace '@MINOR@', mod_version_minor
        replaceIn 'lib/Versions.java'
    replace '@MC_VERSION@', version
        replaceIn 'lib/Versions.java'
    if (System.getenv("CIRCLE_BUILD_NUM") != null) {
        replace '@BUILD_NUMBER@', System.getenv("CIRCLE_BUILD_NUM")
    } else {
        replace '@BUILD_NUMBER@', 0
    }
}

dependencies {
//    deobfCompile "cofh:RedstoneFlux:1.12-2.0.1.2:universal"
    //deobfCompile "mezz.jei:jei_${mc_version}:${jei_version}"
    deobfCompile "cofh:CoFHCore:1.12-${cofhcore_version}:deobf"
    deobfCompile "MCMultiPart2:MCMultiPart:${mcmultipart_version}"
    deobfCompile "li.cil.oc:OpenComputers:MC1.12.1-${oc_version}:api"
    deobfCompile "dan200.computercraft:ComputerCraft-SquidDev-CC-ComputerCraft-feature-minecraft-1.12.2:${computercraft_version}"
    deobfCompile "mcp.mobius.waila:Hwyla:${hwyla_version}"
    deobfCompile "mcjty.theoneprobe:TheOneProbe-1.12:${top_version}"
	deobfProvided "CraftTweaker2:CraftTweaker2-API:${crafttweaker_version}"
	deobfProvided "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-${crafttweaker_version}"
    compile "igwmod:IGW-Mod-1.12.2:${igwmod_version}:userdev"
    deobfCompile("appeng:appliedenergistics2:${ae2_version}:api") {
        transitive = false
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

idea { module { inheritOutputDirs = true } }

